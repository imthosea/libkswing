plugins {
    id "java-library"
    id "org.jetbrains.kotlin.jvm" version "2.1.20"
    id "org.jetbrains.kotlin.plugin.allopen" version "2.1.21"
    id 'org.jetbrains.dokka' version "2.0.0"
    id "maven-publish"
}

group = "me.thosea"
version = "1.0.0"

repositories {
    mavenCentral()
}

kotlin {
    jvmToolchain(17)
    allOpen {
        annotation("me.thosea.kswing.other.FullyOpen")
    }
}

java {
    withSourcesJar()
}

processResources {
    from "LICENSE"
}
sourcesJar {
    from "LICENSE"
}

tasks.register('dokkaJavadocJar', Jar.class) {
    dependsOn(dokkaJavadoc)
    from(dokkaJavadoc)
    archiveClassifier.set("javadoc")
}

tasks.register('dokkaHtmlJar', Jar.class) {
    dependsOn(dokkaHtml)
    from(dokkaHtml)
    archiveClassifier.set("html-docs")
}

publishing {
    repositories {
        maven {
            name = "teamCelestialPublic"
            url = "https://maven.teamcelestial.org/public"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            artifact(dokkaJavadocJar)
            artifact(dokkaHtmlJar)
        }
    }
}